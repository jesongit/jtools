import winex;
import win.ui;
import plugins;
import console
import key.hotkey
import win.ui.menu;
import win.util.tray;
import win.ui.accelerator;
/*DSG{{*/
mainForm = win.form(text="jtools";right=499;bottom=49;acceptfiles=1;border="none";exmode="toolwindow")
mainForm.add(
custom={cls="custom";text="自定义控件";left=0;top=50;right=500;bottom=400;acceptfiles=1;hide=1;repeat="center";z=2};
edit={cls="edit";left=0;top=0;right=500;bottom=50;autovscroll=false;edge=1;font=LOGFONT(h=-35);z=1}
)
/*}}*/

// 载入可能用到的子窗口
var listWin = mainForm.custom.loadForm("\dlg\list.aardio")
var setLink = mainForm.custom.loadForm("\dlg\setLink.aardio")
var settingWin = mainForm.loadForm("\dlg\setLink.aardio");
var curSubWin

mainForm.custom.orphanWindow()	// 设置为悬浮窗口

console.log()

var mod = 'default'
var result = {}

// 选择快捷键
selectOne = function(index){
	if(result[[index]])
		plugins.open(mod, result[index])
}

// 显示子窗口
showSubWin = function(subWin){
	subWin.show()
	mainForm.custom.show()
	mainForm.edit.setFocus()
}

// 关闭子窗口
hideSubWin = function(){
	mainForm.custom.show(false)
	if mainForm.edit.text != ""
		mainForm.edit.text = ""
}

// 打开设置
openSetting = function(){
	settingWin.show();
	settingWin.doModal()
}

// 显示/隐藏界面
switchMain = function(){
	if(winex.isVisible(mainForm.hwnd)) {
		hideSubWin()
		mainForm.show(false)
	}
	else{
		mainForm.show()
		mainForm.edit.setFocus()
	}
}

// 托盘菜单
var trayCfg = {
	[0x205/*_WM_RBUTTONUP*/  ] = function(wParam){
	    win.setForeground(mainForm.hwnd,true)
		mainForm.popmenu = win.ui.popmenu(mainForm);
		mainForm.popmenu.add('&退出',function(id){ mainForm.close() })
	    mainForm.popmenu.popup();
	    mainForm.popmenu.close();
	};
	[0x202/*_WM_LBUTTONUP*/] = function(wParam){ switchMain()};
}
var acceleratorCfg = {
    { alt = true; vkey = 'Q'#; oncommand = function() selectOne(1); };
    { alt = true; vkey = 'W'#; oncommand = function() selectOne(2); };
    { alt = true; vkey = 'E'#; oncommand = function() selectOne(3); };
    { alt = true; vkey = 'R'#; oncommand = function() selectOne(4); };
}

// 注册全局快捷键
mainForm.reghotkey( function(id,mod,vk){
	switchMain()
},1/*_MOD_ALT*/,0x20/*_VK_SPACE*/)
mainForm.enableDpiScaling();

// 窗口快捷键
var accelerator = win.ui.accelerator(acceleratorCfg, mainForm);

// 托盘图标
mainForm.tray = win.util.tray(mainForm)
mainForm.tray.tip = "jtools" //设置鼠标提示
mainForm.onTrayMessage = trayCfg

// 根据文本框变化调用对应接口
mainForm.edit.onChange = function(){
	var text = string.trim(mainForm.edit.text)
	if(text == "") {
		mainForm.custom.show(false)
		return
	}
	result = plugins.search(mod, text)
	//console.dump(result, #result)
	if(..table.len(result)){
		var items = {}
		for(_, v in result)
			..table.push(items, v["name"])
		listWin.listbox.items = items
		showSubWin(listWin)
	}
}

// 按下回车的操作
mainForm.edit.onOk = function(){ selectOne(1) }

// 按下 Esc 的操作
mainForm.edit.onCancel = function(){
	// console.log("is_empty", mainForm.edit.text == '')
	if(mainForm.edit.text == '')
		win.quitMessage()
	else
		hideSubWin()
}

// 拖入文件后的操作
mainForm.onDropFiles = function(files){
	setLink.path.text = files[1]
	showSubWin(setLink)
}

mainForm.edit.setFocus()
mainForm.show()
return win.loopMessage();